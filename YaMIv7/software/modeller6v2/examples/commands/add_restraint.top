# Example for: ADD_RESTRAINT, DELETE_RESTRAINT

# This will enforce cis conformation for Pro-56.

# Make a model and stereochemical restraints:
SET OUTPUT_CONTROL = 1 1 1 1 0

DEFINE_STRING  VARIABLES = ATOM_IDS1 ATOM_IDS2
READ_TOPOLOGY FILE = '$(LIB)/top_heav.lib'
READ_PARAMETERS FILE = '$(LIB)/par.lib'
READ_MODEL FILE = '1fas'
SEQUENCE_TO_ALI ATOM_FILES = '1fas', ALIGN_CODES = '1fas'
SEQUENCE_TO_ALI ADD_SEQUENCE = on, ATOM_FILES = ATOM_FILES '1fas.ini', ;
                ALIGN_CODES = ALIGN_CODES '1fas-ini'
GENERATE_TOPOLOGY SEQUENCE = '1fas-ini'
TRANSFER_XYZ
BUILD_MODEL INITIALIZE_XYZ = off
MAKE_RESTRAINTS RESTRAINT_TYPE = 'stereo'

# Change the Pro-56 restraint from trans to cis:
CALL ROUTINE = 'cispeptide', ATOM_IDS1 =  'O:56' 'C:56' 'N:57' 'CA:57', ;
                             ATOM_IDS2 = 'CA:56' 'C:56' 'N:57' 'CA:57'
WRITE_RESTRAINTS FILE = '1fas.rsr'
ENERGY

SUBROUTINE ROUTINE = 'cispeptide'
  # Delete the old restraint on the same atoms:
  DELETE_RESTRAINT ATOM_IDS = ATOM_IDS1
  # Add the new restraint:
  ADD_RESTRAINT RESTRAINT_PARAMETERS = 3 1 3 3 4 2 0 3.141593 0.087

  DELETE_RESTRAINT ATOM_IDS = ATOM_IDS2
  ADD_RESTRAINT RESTRAINT_PARAMETERS = 3 1 3 3 4 2 0 0.0 0.087

  RETURN
END_SUBROUTINE
